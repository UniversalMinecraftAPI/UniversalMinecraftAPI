buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'java'

    version = '0.1-SNAPSHOT'
    archivesBaseName = "$rootProject.name-$project.name"

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'com.google.guava:guava:19.0'
        testCompile 'org.apache.commons:commons-lang3:3.4'
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    task sourcesJar(type: Jar) {
        from sourceSets.main.java.srcDirs
        classifier = 'src'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc.options {
        links 'http://docs.oracle.com/javase/8/docs/api/'
    }

    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'
}

configure(subprojects.findAll { it.name == 'spigot' || it.name == 'sponge' }) {
    apply plugin: 'com.github.johnrengelman.shadow'

    configurations {
        fatJar
        compile.extendsFrom fatJar
    }

    dependencies {
        fatJar project(':common')
    }

    shadowJar {
        configurations = [project.configurations.fatJar]

        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude 'META-INF/MANIFEST.MF'
        exclude 'about.html'

        relocate 'javax.servlet', 'com.koenv.universalminecraftapi.thirdparty.javax.servlet'
        relocate 'org.eclipse.jetty', 'com.koenv.universalminecraftapi.thirdparty.org.eclipse.jetty'
        relocate 'spark', 'com.koenv.universalminecraftapi.thirdparty.spark'
        relocate 'org.jetbrains.annotations', 'com.koenv.universalminecraftapi.thirdparty.org.jetbrains.annotations'
        relocate 'org.intellij.lang.annotations', 'com.koenv.universalminecraftapi.thirdparty.org.intellij.lang.annotations'
    }

    task dist() {
        dependsOn javadocJar
        dependsOn sourcesJar
        dependsOn shadowJar
    }
}

task dist() {
    dependsOn 'docgenerator:generateDoc'
}

task clean() {
    delete buildDir
}