apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
	fatJar
	compile {
		extendsFrom fatJar
	}
}

dependencies {
    fatJar project(':common')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'src'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task allJar(type: Jar) {
    from sourceSets.main.output
    from configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }

    classifier = 'all'

    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
    exclude 'META-INF/MANIFEST.MF'
}

task dist() {
	dependsOn javadocJar
	dependsOn sourcesJar
	dependsOn allJar
}